; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Sylpheed-Claws
AppVerName=Sylpheed-@VERSION@ Win32
AppPublisher=The friendly Sylpheed hackers
AppPublisherURL=http://claws-w32.sourceforge.net
AppSupportURL=http://claws-w32.sourceforge.net
AppUpdatesURL=http://claws-w32.sourceforge.net
DefaultDirName={code:GetInstalledDir}
DefaultGroupName=Sylpheed-Claws
AllowNoIcons=yes
;AlwaysCreateUninstallIcon=yes
LicenseFile=@PAKDIR@\doc\COPYING
InfoAfterFile=@PAKDIR@\doc\README
; uncomment the following line if you want your installation to run on NT 3.51 too.
; MinVersion=4,3.51
DisableStartupPrompt  =yes
OutputBaseFilename    =sylpheed-@VERSION@
;Compression=none
Compression=bzip/9

[Types]
Name: "full";                   Description: "Full installation"
Name: "compact";                Description: "Compact installation"
Name: "custom";                 Description: "Custom installation";                     Flags: iscustom;

[Components]
Name: "main";                   Description: "Sylpheed program files";                  Types: full compact custom;     Flags: fixed;
Name: "settings";               Description: "Sylpheed registry settings";              Types: full compact custom;
Name: "help";                   Description: "Sylpheed help files";                     Types: full custom;
Name: "themes";                 Description: "Sylpheed theme pack";                     Types: full;
Name: "gtk";                    Description: "Gtk+ libraries (customized build, v1.3 + v2.0)";  Types: full compact custom;

[Tasks]
Name: "sendtoicon";             Description: "Create a &send-to link";                          GroupDescription: "Additional icons:"
Name: "desktopicon";            Description: "Create a &desktop icon";                          GroupDescription: "Additional icons:"
Name: "quicklaunchicon";        Description: "Create a &Quick Launch icon";                     GroupDescription: "Additional icons:";

Name: "reg_sylpheed";           Description: "Create Sylpheed &registry key (recommended)";     GroupDescription: "Registry settings:"; Components: settings;
Name: "reg_defaultmailer";      Description: "Set Sylpheed default &mailer for mailto: links";  GroupDescription: "Registry settings:"; Components: settings;

[Files]
Source: "@PAKDIR@\bin\sylpheed.exe";          DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\sylpheed.dll";          DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\fnmatch.dll";           DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\libcompface.dll";       DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\regex.dll";             DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\w32lib.dll";            DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\iconv.dll";             DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\crypt.dll";             DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\pthreadVC.dll";         DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;

Source: "@PAKDIR@\bin\gspawn-win32-helper.exe";  DestDir: "{app}\bin";                CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\libgthread-2.0-0.dll";  DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\libgdk-0.dll";          DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\libglib-2.0-0.dll";     DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\libgmodule-2.0-0.dll";  DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\libgobject-2.0-0.dll";  DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\libgtk-0.dll";          DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\intl.dll";              DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\asprintf.dll";          DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk
Source: "@PAKDIR@\bin\charset.dll";           DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite; Components: gtk

Source: "@PAKDIR@\Readme.html";               DestDir: "{app}";                       CopyMode: alwaysoverwrite
Source: "@PAKDIR@\Readme.txt";                DestDir: "{app}";                       CopyMode: alwaysoverwrite
Source: "@PAKDIR@\doc\*.*";                   DestDir: "{app}\doc";                   CopyMode: alwaysoverwrite
Source: "@PAKDIR@\doc\faq\*.*";               DestDir: "{app}\doc\faq";               CopyMode: alwaysoverwrite; Flags: recursesubdirs; Components: help
Source: "@PAKDIR@\doc\manual\*.*";            DestDir: "{app}\doc\manual";            CopyMode: alwaysoverwrite; Flags: recursesubdirs; Components: help
Source: "@PAKDIR@\etc\*.*";                   DestDir: "{app}\etc";                   CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "@PAKDIR@\locale\*.*";                DestDir: "{app}\locale";                CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "@PAKDIR@\themes\*.*";                DestDir: "{app}\themes";                CopyMode: alwaysoverwrite; Flags: recursesubdirs; Components: themes
Source: "@PAKDIR@\openssl\*.*";               DestDir: "{app}\openssl";               CopyMode: alwaysoverwrite; Flags: recursesubdirs
; Claws specific
Source: "@PAKDIR@\bin\libeay32.dll";          DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\ssleay32.dll";          DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\openssl.exe";           DestDir: "{app}\bin";                   CopyMode: alwaysoverwrite;
; Plugins
Source: "@PAKDIR@\bin\plugins\demo.dll";              DestDir: "{app}\bin\plugins";   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\plugins\spamassassin.dll";      DestDir: "{app}\bin\plugins";   CopyMode: alwaysoverwrite;
Source: "@PAKDIR@\bin\plugins\spamassassin_gtk.dll";  DestDir: "{app}\bin\plugins";   CopyMode: alwaysoverwrite;

[INI]
Filename: "{app}\doc\gtk-w32.url";                              Section: "InternetShortcut"; Key: "URL"; String: "http://www.gimp.org/~tml/gimp/win32/"
Filename: "{app}\doc\sylpheed.url";                             Section: "InternetShortcut"; Key: "URL"; String: "http://sylpheed.good-day.net"
Filename: "{app}\doc\sylpheed-w32-en.url";                      Section: "InternetShortcut"; Key: "URL"; String: "http://claws-w32.sf.net"
Filename: "{app}\doc\sylpheed-w32-jp.url";                      Section: "InternetShortcut"; Key: "URL"; String: "http://www2.odn.ne.jp/munesato/sylpheed"
Filename: "{app}\doc\sylpheed-claws.url";                       Section: "InternetShortcut"; Key: "URL"; String: "http://sylpheed-claws.sf.net"
Filename: "{app}\doc\claws-sf-project.url";                     Section: "InternetShortcut"; Key: "URL"; String: "http://sf.net/projects/sylpheed-claws"
Filename: "{app}\doc\claws-cvs.url";                            Section: "InternetShortcut"; Key: "URL"; String: "http://cvs.sf.net/cgi-bin/viewcvs.cgi/sylpheed-claws/sylpheed-claws/"
Filename: "{app}\doc\claws-cvs-w32.url";                        Section: "InternetShortcut"; Key: "URL"; String: "http://cvs.sf.net/cgi-bin/viewcvs.cgi/sylpheed-claws/sylpheed-claws/?only_with_tag=win32"
Filename: "{app}\doc\claws-patches.url";                        Section: "InternetShortcut"; Key: "URL"; String: "http://www.thewildbeast.co.uk/sylpheed/"

[Icons]
Name: "{sendto}\Sylpheed-Claws";                                Filename: "{app}\bin\sylpheed.exe"; Parameters: "--attach"; WorkingDir: "{app}\bin"; MinVersion: 4,4; Tasks: sendtoicon
Name: "{userdesktop}\Sylpheed-Claws";                           Filename: "{app}\bin\sylpheed.exe"; WorkingDir: "{app}\bin"; MinVersion: 4,4; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\Sylpheed-Claws"; Filename: "{app}\bin\sylpheed.exe"; WorkingDir: "{app}\bin"; MinVersion: 4,4; Tasks: quicklaunchicon

Name: "{group}\Sylpheed-Claws";                                 Filename: "{app}\bin\sylpheed.exe"; WorkingDir: "{app}\bin"
Name: "{group}\Uninstall Sylpheed for Win32";                   Filename: "{uninstallexe}"
Name: "{group}\Win32-Readme (html)";                            Filename: "{app}\Readme.html";
Name: "{group}\Win32-Readme (text)";                            Filename: "{app}\Readme.txt";
Name: "{group}\Sylpheed-FAQ";                                   Filename: "{app}\doc\faq\en\sylpheed-faq.html";         Components: help;
Name: "{group}\Sylpheed-Manual";                                Filename: "{app}\doc\manual\en\sylpheed.html";          Components: help;

Name: "{group}\links\Sylpheed";                                 Filename: "{app}\doc\sylpheed.url"
Name: "{group}\links\Sylpheed (Win32) english";                 Filename: "{app}\doc\sylpheed-w32-en.url"
Name: "{group}\links\Sylpheed (Win32) japanese";                Filename: "{app}\doc\sylpheed-w32-jp.url"
Name: "{group}\links\Gtk+ for Win32";                           Filename: "{app}\doc\gtk-w32.url"
Name: "{group}\links\Sylpheed-Claws";                           Filename: "{app}\doc\sylpheed-claws.url"
Name: "{group}\links\Sylpheed-Claws (Win32)";                   Filename: "{app}\doc\sylpheed-claws-w32.url"
Name: "{group}\links\Sylpheed-Claws SourceForge project";       Filename: "{app}\doc\claws-sf-project.url"
Name: "{group}\links\Sylpheed-Claws CVS tree";                  Filename: "{app}\doc\claws-cvs.url"
Name: "{group}\links\Sylpheed-Claws CVS tree (Win32)";          Filename: "{app}\doc\claws-cvs-w32.url"
Name: "{group}\links\Sylpheed-patches";                         Filename: "{app}\doc\claws-patches.url"

[Registry]
Root: HKCU; Subkey: "Software\Sylpheed";                        Flags: uninsdeletekeyifempty; Tasks: reg_sylpheed
Root: HKCU; Subkey: "Software\Sylpheed";                                                        ValueType: string;      ValueName: "InstalledDir";      ValueData: "{app}";                     Tasks: reg_sylpheed
Root: HKCU; Subkey: "Software\Sylpheed";                                                        ValueType: string;      ValueName: "HomeDir";           ValueData: "{code:GetHomeDir}";         Tasks: reg_sylpheed
Root: HKCU; Subkey: "Software\Sylpheed";                                                        ValueType: dword;       ValueName: "MajorVersion";      ValueData: "@MAJOR_VERSION@";                         Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Sylpheed";                                                        ValueType: dword;       ValueName: "MinorVersion";      ValueData: "@MINOR_VERSION@";                         Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Sylpheed";                                                        ValueType: dword;       ValueName: "MicroVersion";      ValueData: "@MICRO_VERSION@";                         Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Sylpheed";                                                        ValueType: string;      ValueName: "UninstallString";   ValueData: "{uninstallexe}";            Flags: uninsdeletekey

Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\Sylpheed.exe";     Flags: uninsdeletekey; Tasks: reg_sylpheed
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\Sylpheed_d.exe";   Flags: uninsdeletekey; Tasks: reg_sylpheed
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\Sylpheed.exe";         ValueType: string;      ValueName: "{app}\bin\Sylpheed.exe";          ValueData: "{app}\bin\Sylpheed.exe";    Tasks: reg_sylpheed
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\Sylpheed_d.exe";       ValueType: string;      ValueName: "{app}\bin\Sylpheed_d.exe";        ValueData: "{app}\bin\Sylpheed.exe";    Tasks: reg_sylpheed
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\Sylpheed.exe";         ValueType: string;      ValueName: "Path";              ValueData: "{app}\bin;{code:AspellDir};"; Tasks: reg_sylpheed
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\Sylpheed_d.exe";       ValueType: string;      ValueName: "Path";              ValueData: "{app}\bin;{code:AspellDir};"; Tasks: reg_sylpheed

Root: HKCR; Subkey: "mailto";                                   Flags: uninsclearvalue; Tasks: reg_defaultmailer
Root: HKCR; Subkey: "mailto";                                                                   ValueType: string;      ValueName: "";                  ValueData: "URL:MailTo-Protocol";                       Flags: uninsclearvalue; Tasks: reg_defaultmailer
Root: HKCR; Subkey: "mailto";                                                                   ValueType: string;      ValueName: "URL Protocol";      ValueData: "";                                          Flags: uninsclearvalue; Tasks: reg_defaultmailer
Root: HKCR; Subkey: "mailto";                                                                   ValueType: binary;      ValueName: "EditFlags";         ValueData: "02 00 00 00";                               Flags: uninsclearvalue; Tasks: reg_defaultmailer
Root: HKCR; Subkey: "mailto\DefaultIcon";                                                       ValueType: string;      ValueName: "";                  ValueData: "{app}\bin\sylpheed.exe,0";                  Flags: uninsclearvalue; Tasks: reg_defaultmailer
Root: HKCR; Subkey: "mailto\shell\open\command";                                                ValueType: string;      ValueName: "";                  ValueData: "{app}\bin\sylpheed.exe --compose ""%1""";   Flags: uninsclearvalue; Tasks: reg_defaultmailer

Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed";           Flags: uninsdeletekey; Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail";                                                    ValueType: string;      ValueName: "";                  ValueData: "Sylpheed";                                  Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed";                                           ValueType: string;      ValueName: "";                  ValueData: "Sylpheed";                                  Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed";                                           ValueType: string;      ValueName: "DLLPath";           ValueData: "";                                          Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed\Protocols\mailto";                          ValueType: string;      ValueName: "";                  ValueData: "URL:MailTo-Protokoll";                      Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed\Protocols\mailto";                          ValueType: string;      ValueName: "URL Protocol";      ValueData: "";                                          Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed\Protocols\mailto";                          ValueType: binary;      ValueName: "EditFlags";         ValueData: "02 00 00 00";                               Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed\Protocols\mailto\DefaultIcon";              ValueType: string;      ValueName: "";                  ValueData: "{app}\bin\sylpheed.exe";                    Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed\Protocols\mailto\shell\open\command";       ValueType: string;      ValueName: "";                  ValueData: "{app}\bin\sylpheed.exe --compose ""%1""";   Tasks: reg_defaultmailer
Root: HKLM; Subkey: "SOFTWARE\Clients\Mail\Sylpheed\shell\mailto\shell\open\command";           ValueType: string;      ValueName: "";                  ValueData: "{app}\bin\sylpheed.exe";                    Tasks: reg_defaultmailer

;[Run]
;Filename: "{app}\bin\sylpheed.exe";    Description: "Launch Sylpheed-Claws"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{app}\doc\sylpheed.url"
Type: files; Name: "{app}\doc\sylpheed-w32-en.url"
Type: files; Name: "{app}\doc\sylpheed-w32-jp.url"
Type: files; Name: "{app}\doc\sylpheed-claws.url"
Type: files; Name: "{app}\doc\claws-sf-project.url"
Type: files; Name: "{app}\doc\claws-cvs.url"
Type: files; Name: "{app}\doc\claws-cvs-w32.url"
Type: files; Name: "{app}\doc\claws-patches.url"
Type: files; Name: "{app}\doc\gtk-w32.url"
Type: dirifempty; Name: "{app}\bin\"
Type: dirifempty; Name: "{app}\doc\"
Type: dirifempty; Name: "{app}"

[Dirs]
Name: {code:GetHomeDir}

[Code]
var
  SylpheedHomeDir : string;
  SylpheedInstalledDir : string;

function GetHomeDir(default:string):string;
begin
  { try to re-use last home dir }
  if SylpheedHomeDir = ''
  then RegQueryStringValue(HKCU, 'Software\Sylpheed', 'HomeDir', SylpheedHomeDir);
  { otherwise use application data folder }
  if (SylpheedHomeDir = '')
  then SylpheedHomeDir := ExpandConstant('{userappdata}');
  Result := SylpheedHomeDir ;
end;

function GetInstalledDir(dummydefault:string):string;
begin
  { try to re-use last install dir }
  if SylpheedInstalledDir = ''
  then RegQueryStringValue(HKCU, 'Software\Sylpheed', 'InstalledDir', SylpheedInstalledDir);
  { otherwise use program files dir }
  if (SylpheedInstalledDir = '')
  then SylpheedInstalledDir := ExpandConstant('{pf}\Sylpheed-Claws');
  Result := SylpheedInstalledDir
end;

function SelectHomeDir(var home:string):boolean;
begin
  ScriptDlgPageSetCaption('Select HOME directory');
  ScriptDlgPageSetSubCaption1('Where should the configuration an mailbox folders be stored?');
  ScriptDlgPageSetSubCaption2('(Read the Readme.txt on how to change this locations later)');
  ScriptDlgPageShowBackButton(true);
  Result := InputDir('', home);
  ScriptDlgPageClose(true);
end;

function SylpheedInstalled(var version,uninstallcmd:string):boolean;
var maj,min,mic:Cardinal;
begin
  Result := RegQueryDWordValue(HKCU, 'Software\Sylpheed', 'MajorVersion', maj)
        and RegQueryDWordValue(HKCU, 'Software\Sylpheed', 'MinorVersion', min)
        and RegQueryDWordValue(HKCU, 'Software\Sylpheed', 'MicroVersion', mic);
  RegQueryStringValue(HKCU, 'Software\Sylpheed', 'UninstallString', uninstallcmd)
  version:=IntToStr(maj)+'.'+IntToStr(min)+'.'+IntToStr(mic);
end;

function AspellDir(default:string):string;
var dir:string;
begin
  RegQueryStringValue(HKLM, 'Software\Aspell', '', dir)
  Result := dir;
end;

function AspellInstalled():boolean;
begin
  Result:=AspellDir('')<>'';
end;

function GPGInstalled(var HomeDir,gpgProgram,InstalledDir:string):boolean;
var
    HomeSet,
    InstSet,
    ProgSet:boolean;
begin
  HomeSet := RegQueryStringValue(HKCU, 'Software\GNU\GNUPG',         'HomeDir',      HomeDir);
  ProgSet := RegQueryStringValue(HKCU, 'Software\GNU\GNUPG',         'gpgProgram',   gpgProgram);
  InstSet := RegQueryStringValue(HKCU, 'Software\GNU\GNUPG\HomeDir', 'InstalledDir', InstalledDir);
  if (not ProgSet)
  then
  begin
    ProgSet:=true;
    if InstSet
    then gpgProgram:=InstalledDir+'\gpg.exe'
    else if HomeSet
    then gpgProgram:=HomeDir+'\gpg.exe'
    else ProgSet := false;
    if ProgSet
    then RegWriteStringValue(HKCU, 'Software\GNU\GNUPG', 'gpgProgram', gpgProgram);
  end;
  Result := ProgSet;
end;

function InitializeSetup(): Boolean;
var version,
    uninst    :string;
    msgres,
    execres   :integer;
    s1,s2,s3:string;
begin
  Result:=true;
  GetInstalledDir('');
  GetHomeDir('');

  if SylpheedInstalled(version,uninst)
  then
  begin
    msgres:=MsgBox('Sylpheed-'+version+' is currently installed.'+#13#13
                   +'Do you want to uninstall it first?.',
                   mbError, MB_YESNOCANCEL);
    case msgres of
      IdYes:      begin
                    InstExec(uninst, '', '', true, true, SW_SHOWNORMAL, execres);
                    Result:=InitializeSetup();
                  end;
      IdCancel:   Result:=false;
      IdNo:       ;
    end;
    
    if Result and not GPGInstalled(s1,s2,s3)
    then if MsgBox('GPG was not found on your system.'+#13#13
                   +'If you want to use encrypted mails,'+#13
                   +'you should install GnuPG first.'+#13#13
                   +'http://www.gnupg.org (original)'+#13
                   +'http://www.nullify.org (patched)'+#13#13
                   +'Do you want to abort?'+#13,
                   mbError, MB_YESNO) = idNo
         then Result := true
         else Result := false;
    
    if Result and not AspellInstalled()
    then if MsgBox('Aspell was not found on your system.'+#13#13
                   +'If you want to use spellchecking,'+#13
                   +'you should download and install it first.'+#13#13
                   +'GNU-Aspell/Win32'+#13
                   +'http://aspell.net/win32'+#13#13
                   +'Do you want to abort?'+#13,
                   mbError, MB_YESNO) = idNo
         then Result:=true
         else Result:=false;
  end;
end;

const CR = #13 ;
function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo, MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
begin
  { also display chosen HOME directory }
  if MemoUserInfoInfo <> ''
  then Result := MemoUserInfoInfo + CR + CR ;
  Result := Result + MemoDirInfo + CR + CR
                   + 'Home directory:' + CR
                   + Space + SylpheedHomeDir + CR + CR;
  if MemoTypeInfo <> ''
  then Result := Result + MemoTypeInfo + CR + CR ;
  if MemoComponentsInfo <> ''
  then Result := Result + MemoComponentsInfo + CR + CR ;
  if MemoGroupInfo <> ''
  then Result := Result + MemoGroupInfo + CR + CR ;
  if MemoTasksInfo <> ''
  then Result := Result + MemoTasksInfo + CR + CR ;
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
  { insert home-dir query between install-dir and components page }
  if CurPage = wpSelectDir
  then Result := SelectHomeDir(SylpheedHomeDir)
  else Result := true;
end;

function BackButtonClick(CurPage: Integer): Boolean;
begin
  { insert home-dir query between install-dir and components page }
  if CurPage = wpSelectComponents
  then Result := not SelectHomeDir(SylpheedHomeDir)
  else Result := true;
end;

